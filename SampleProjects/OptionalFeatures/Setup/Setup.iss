; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Sample Extensible App"
#define MyAppExeName "ExtensibleApp.exe"
#define MyAppVersion GetFileVersion('..\Bin\' + MyAppExeName)
#define MyAppPublisher "Acme Technology"
#define MyAppURL "http://www.acme.com/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{0AB43A4D-14D5-4FD8-8BF6-C432A5FB0B81}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
AppContact=contact@acme.com
AppSupportPhone=+902122222222
AppCopyright={#MyAppPublisher}
WizardImageFile=Setup_Banner.bmp
WizardSmallImageFile=compiler:WizModernSmallImage-IS.bmp
WizardImageBackColor=$272727
WizardImageStretch=false
LicenseFile=End User Licence Agreement.rtf
UsePreviousAppDir=False
DefaultDirName={pf32}\{#MyAppPublisher}\{#MyAppName}\
DirExistsWarning=False
EnableDirDoesntExistWarning=False
AppendDefaultDirName=False
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename={#MyAppName} {#MyAppVersion}
Compression=lzma
SolidCompression=yes
UninstallDisplayName={#MyAppName} {#MyAppVersion}
UninstallDisplayIcon={app}\SetupIcon.ico
;Uncomment following lines if this is a 64 bit application
;ArchitecturesInstallIn64BitMode=x64
;ArchitecturesAllowed=x64
MinVersion=0,6.1

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Types]
Name: "full"; Description: "Full installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Components]
Name: "Feature1"; Description: "Feature 1"; Types: full custom;
Name: "Feature2"; Description: "Feature 2"; Types: full custom;
Name: "Feature3"; Description: "Feature 3"; Types: full custom;

[Files]
Source: "..\Bin\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Bin\{#MyAppExeName}.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "SetupIcon.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Bin\Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Bin\Feature1.Plugin.dll"; DestDir: "{app}"; Components: Feature1; Flags: ignoreversion
Source: "..\Bin\Feature2.Plugin.dll"; DestDir: "{app}"; Components: Feature2; Flags: ignoreversion
Source: "..\Bin\Feature3.Plugin.dll"; DestDir: "{app}"; Components: Feature3; Flags: ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\SetupIcon.ico";
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; IconFilename: "{app}\SetupIcon.ico";
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\SetupIcon.ico";

[Code]
////// INSTALLED CONTROL ///////////////////
function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

function IsUpgrade(): Boolean;
begin
  Result := (GetUninstallString() <> '');
end;

function InitializeSetup(): Boolean;
begin
    Result:=true;
    if (IsUpgrade()) then
    begin
      MsgBox('Product is already installed. Please uninstall and try again.', mbInformation, MB_OK);
      Result := false;
    end;
end;
////// INSTALLED CONTROL ///////////////////

